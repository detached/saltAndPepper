plugins {
    id "org.jetbrains.kotlin.jvm" version "1.8.10"
    id "org.jetbrains.kotlin.kapt" version "1.8.10"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.8.10"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.micronaut.application" version '3.7.5'
    id 'org.flywaydb.flyway' version '9.19.1'
    id 'nu.studer.jooq' version '8.1'
    id 'com.github.node-gradle.node' version '3.5.1'
}

version "1.0"
group "de.w3is"
mainClassName = "de.w3is.recipes.ApplicationKt"

repositories {
    mavenCentral()
}

configurations {
    flywayMigration
}

ext {
    kotlinVersion = "1.8.10"
    postgresqlJdbcVersion = "42.6.0"
    jooqVersion = "3.18.0"
    nodeVersion = "18.12.0"
    nodeNpmVersion = "8.19.2"
    micronautVersion = "3.7.4"
    javaxTransactionApiVersion = "1.3"
    assertKVersion = "0.25"
    thumbnailatorVersion = "0.4.19"
    commonsTextVersion = "1.10.0"
    springSecurityVersion = "6.0.2"
    mockitoVersion = "4.1.0"
}

dependencies {
    flywayMigration("org.postgresql:postgresql:${postgresqlJdbcVersion}")

    jooqGenerator("org.jooq:jooq-meta-extensions:${jooqVersion}")

    kapt("io.micronaut:micronaut-http-validation")

    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("javax.transaction:javax.transaction-api:${javaxTransactionApiVersion}")

    implementation("io.micronaut.kotlin:micronaut-kotlin-runtime")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut.security:micronaut-security")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.security:micronaut-security-annotations")

    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.sql:micronaut-jooq")

    implementation("io.projectreactor:reactor-core")
    implementation("org.springframework.security:spring-security-core:${springSecurityVersion}")
    implementation("net.coobird:thumbnailator:${thumbnailatorVersion}")
    implementation("org.apache.commons:commons-text:${commonsTextVersion}")

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("com.fasterxml.jackson.module:jackson-module-kotlin")
    runtimeOnly("com.h2database:h2")
    runtimeOnly("org.postgresql:postgresql:${postgresqlJdbcVersion}")

    kaptTest("io.micronaut:micronaut-inject-java")
    testImplementation("com.willowtreeapps.assertk:assertk:${assertKVersion}")
    testImplementation("org.mockito.kotlin:mockito-kotlin:${mockitoVersion}")
}

node {
    download = true
    version = nodeVersion
    npmVersion = nodeNpmVersion
    nodeProjectDir = file("${project.projectDir}/frontend/")
}

kotlin {
    jvmToolchain(17)
}

compileKotlin {
    compilerOptions {
        freeCompilerArgs = ["-Xcontext-receivers"]
    }
}

compileTestKotlin {
    compilerOptions {
        freeCompilerArgs = ["-Xcontext-receivers"]
    }
}

kapt {
    arguments {
        arg("micronaut.processing.incremental", true)
        arg("micronaut.processing.annotations", "de.w3is.recipes.*")
    }
}

micronaut {
    version micronautVersion
    runtime "netty"
    testRuntime "junit5"
    processing {
        incremental true
        annotations "de.w3is.recipes.*"
    }
}

flyway {
    configurations = ['flywayMigration']
    url = "jdbc:postgresql://localhost:5432/recipe"
    user = "user"
    password = "password"
}

jooq {
    version = jooqVersion
    configurations {
        main {
            generationTool {
                generator {
                    name = 'org.jooq.codegen.KotlinGenerator'
                    database {
                        name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                        properties {
                            property {
                                key = 'scripts'
                                value = 'src/main/resources/db/migration/*.sql'
                            }
                            property {
                                key = 'sort'
                                value = 'flyway'
                            }
                            property {
                                key = 'defaultNameCase'
                                value = 'lower'
                            }
                        }
                    }
                    target {
                        packageName = 'de.w3is.recipes.infra.persistence.generated'
                    }
                }
            }
        }
    }
}

tasks.register('buildFrontend', NpmTask) {
    dependsOn(npmInstall)
    args = ['run', 'build']
    inputs.files('frontend/package.json', 'frontend/package-lock.json')
    inputs.dir('frontend/src')
    inputs.dir(fileTree('frontend/node_modules').exclude('.cache'))
    outputs.dir('frontend/build')
}

tasks.register('copyFrontendBundle', Copy) {
    dependsOn(buildFrontend)
    from(file('frontend/build/')) {
        include '**/*'
    }
    into('build/resources/main/public/')
}
processResources.dependsOn(copyFrontendBundle)

tasks.named('generateJooq').configure {
    inputs.files(fileTree('src/main/resources/db/migration'))
            .withPropertyName('migrations')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    allInputsDeclared = true
}