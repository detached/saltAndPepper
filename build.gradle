plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.0"
    id "org.jetbrains.kotlin.kapt" version "1.6.0"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.6.0"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "io.micronaut.application" version '3.0.1'
    id 'org.flywaydb.flyway' version '8.2.1'
    id 'nu.studer.jooq' version '6.0.1'
    id 'org.siouan.frontend-jdk11' version '6.0.0'
}

version "0.1"
group "de.w3is"
mainClassName = "de.w3is.recipes.ApplicationKt"

repositories {
    mavenCentral()
}

configurations {
    flywayMigration
}

dependencies {
    flywayMigration("org.postgresql:postgresql:${postgresqlJdbcVersion}")
    jooqGenerator("org.postgresql:postgresql:${postgresqlJdbcVersion}")

    kapt("io.micronaut:micronaut-http-validation")
    kapt("io.micronaut:micronaut-validation")
    kapt("io.micronaut:micronaut-inject-java")

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    implementation("javax.annotation:javax.annotation-api")
    implementation("javax.transaction:javax.transaction-api:1.3")

    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.kotlin:micronaut-kotlin-runtime")
    implementation("io.micronaut.kotlin:micronaut-kotlin-extension-functions")
    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.security:micronaut-security-session")
    implementation("io.micronaut.security:micronaut-security-annotations")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.sql:micronaut-jooq")
    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("io.micronaut.views:micronaut-views-velocity")
    implementation("io.micronaut.cache:micronaut-cache-core")
    implementation("io.micronaut.cache:micronaut-cache-caffeine")
    implementation("io.projectreactor:reactor-core")
    implementation("org.springframework.security:spring-security-core:${springSecurityVersion}")
    implementation("net.coobird:thumbnailator:${thumbnailatorVersion}")
    implementation("org.apache.commons:commons-text:${commonsTextVersion}")
    implementation("org.apache.velocity:velocity-tools:${velocityToolsVersion}")

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("com.fasterxml.jackson.module:jackson-module-kotlin")
    runtimeOnly("com.h2database:h2")
    runtimeOnly("org.postgresql:postgresql:${postgresqlJdbcVersion}")

    kaptTest("io.micronaut:micronaut-inject-java")
    testImplementation("com.willowtreeapps.assertk:assertk:${assertKVersion}")
}

java {
    sourceCompatibility = JavaVersion.toVersion('11')
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

kapt {
    arguments {
        arg("micronaut.processing.incremental", true)
        arg("micronaut.processing.annotations", "de.w3is.recipes.*")
    }
}

micronaut {
    runtime "netty"
    testRuntime "junit5"
    processing {
        incremental true
        annotations "de.w3is.recipes.*"
    }
}

flyway {
    configurations = ['flywayMigration']
    url = "jdbc:postgresql://localhost:5432/recipe"
    user = "user"
    password = "password"
}

jooq {
    version = '3.14.12'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                jdbc {
                    url = flyway.url
                    user = flyway.user
                    password = flyway.password
                }
                generator {
                    database {
                        inputSchema = 'public'
                    }
                    target {
                        packageName = 'de.w3is.recipes.infra.persistence.generated'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}

frontend {
    yarnVersion = "1.22.10"
    nodeVersion = "14.15.0"

    yarnEnabled = true
    nodeDistributionProvided = false

    packageJsonDirectory = file("./src/main/js/")

    assembleScript = "run build"
}

task(copyFrontendBundle, type: Copy) {
    dependsOn assembleFrontend
    from(file('src/main/js/dist/')) {
        include '**/*'
    }
    into('build/resources/main/public/')
}
processResources.dependsOn(copyFrontendBundle)

tasks.named('generateJooq').configure {
    dependsOn tasks.named('flywayMigrate')
    inputs.files(fileTree('src/main/resources/db/migration'))
            .withPropertyName('migrations')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    allInputsDeclared = true
    outputs.cacheIf { true }
}